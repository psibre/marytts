sourceSets {
    lexicon {
        resources {
            srcDir 'lib/modules/ru/lexicon'
        }
    }
}

processLexiconResources {
    filesMatching('ru.txt') {
        // drop lines that are comments or lack a transcription
        def regex = new org.apache.tools.ant.types.RegularExpression()
        regex.pattern = /^\s*[^#\s]+\s+[^\s]+/
        filter(org.apache.tools.ant.filters.LineContainsRegExp, regexps: [regex])
        // remove spaces between phonemes
        filter { it.replaceAll(' ', '') }
    }
}

createLexicon {
    def workingDir = sourceSets.lexicon.output.resourcesDir
    inputs.files file("$workingDir/allophones.ru.xml"), file("$workingDir/ru.txt")
    outputs.files file("$workingDir/ru_lexicon.fst"), file("$workingDir/ru.lts")
}

processResources {
    from createLexicon, {
        rename { "marytts/language/ru/lexicon/$it" }
    }
}

test {
    useTestNG() {
        suiteXmlBuilder().suite(name: 'lexicon') {
            parameter(name: 'allophoneset', value: file('lib/modules/ru/lexicon/allophones.ru.xml'))
            parameter(name: 'lexicon', value: file('lib/modules/ru/lexicon/ru.txt'))
            parameter(name: 'lts', value: '/marytts/language/ru/lexicon/ru.lts')
            parameter(name: 'fst', value: '/marytts/language/ru/lexicon/ru_lexicon.fst')
            test(name: 'lexicon') {
                classes([:]) {
                    'class'(name: 'marytts.language.ru.RussianLTSTest')
                }
            }
        }
    }
}
