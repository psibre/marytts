configurations {
    lexicon
}

sourceSets {
    lexicon {
        resources {
            srcDir 'lib/modules/de/lexicon'
        }
    }
}

dependencies {
    lexiconCompile rootProject.allprojects.find { it.name == 'marytts-builder' }
    lexiconCompile localGroovy()
}

processLexiconResources {
    filesMatching('de.txt') {
        // drop lines that are comments or lack a transcription
        def regex = new org.apache.tools.ant.types.RegularExpression()
        regex.pattern = /^\s*[^#\s]+\s+[^\s]+/
        filter(org.apache.tools.ant.filters.LineContainsRegExp, regexps: [regex])
        // insert pipe delimiter
        filter { it.split(' ', 2).join('|') }
    }
}

task createLexicon {
    dependsOn lexiconClasses
    test.dependsOn it
    def workingDir = sourceSets.lexicon.output.resourcesDir
    inputs.files file("$workingDir/allophones.de.xml"), file("$workingDir/de.txt")
    outputs.files file("$workingDir/de_lexicon.fst"), file("$workingDir/de.lts")
    doLast {
        javaexec {
            main 'marytts.language.LexiconCreator'
            args inputs.files.files + outputs.files.files
            classpath sourceSets.lexicon.runtimeClasspath
        }
    }
}

test {
    useTestNG() {
        suiteXmlBuilder().suite(name: 'lexicon') {
            parameter(name: 'allophoneset', value: file('lib/modules/de/lexicon/allophones.de.xml'))
            parameter(name: 'lexicon', value: file('lib/modules/de/lexicon/de.txt'))
            parameter(name: 'lts', value: file('build/resources/lexicon/de.lts'))
            parameter(name: 'fst', value: file('build/resources/lexicon/de_lexicon.fst'))
            test(name: 'lexicon') {
                classes([:]) {
                    'class'(name: 'marytts.language.de.GermanLTSTest')
                }
            }
        }
    }
}
