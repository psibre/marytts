sourceSets {
    lexicon {
        resources {
            srcDir 'lib/modules/it/lexicon'
        }
    }
}

processLexiconResources {
    filesMatching('it.txt') {
        // drop lines that are comments or lack a transcription
        def regex = new org.apache.tools.ant.types.RegularExpression()
        regex.pattern = /^\s*[^#\s]+\s+[^\s]+/
        filter(org.apache.tools.ant.filters.LineContainsRegExp, regexps: [regex])
        // insert pipe delimiter
        filter { it.split(' ', 3).join('|') }
    }
}

createLexicon {
    def workingDir = sourceSets.lexicon.output.resourcesDir
    inputs.files file("$workingDir/allophones.it.xml"), file("$workingDir/it.txt")
    outputs.files file("$workingDir/it_lexicon.fst"), file("$workingDir/it.lts")
}

processResources {
    from createLexicon, {
        rename { "marytts/language/it/lexicon/$it" }
    }
}

test {
    useTestNG() {
        suiteXmlBuilder().suite(name: 'lexicon') {
            parameter(name: 'allophoneset', value: file('lib/modules/it/lexicon/allophones.it.xml'))
            parameter(name: 'lexicon', value: file('lib/modules/it/lexicon/it.txt'))
            parameter(name: 'lts', value: '/marytts/language/it/lexicon/it.lts')
            parameter(name: 'fst', value: '/marytts/language/it/lexicon/it_lexicon.fst')
            test(name: 'lexicon') {
                classes([:]) {
                    'class'(name: 'marytts.language.it.ItalianLTSTest')
                }
            }
        }
    }
}
