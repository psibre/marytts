plugins {
    id 'groovy'
}

version '5.2-SNAPSHOT'

sourceSets {
    lexicon
    lexiconTest
}

configurations {
    lexicon
}

repositories {
    mavenLocal()
    jcenter()
    ivy {
        url 'http://svn.code.sf.net/p/cmusphinx/code/trunk/'
        layout 'pattern', {
            artifact '[module]/[artifact]-[revision]'
        }
    }
}

dependencies {
    lexicon name: 'cmudict', version: '0.7b'
    lexiconCompile localGroovy()
    lexiconCompile group: 'de.dfki.mary', name: 'marytts-common', version: version
    lexiconCompile group: 'de.dfki.mary', name: 'marytts-transcription', version: version
    lexiconTestCompile localGroovy()
    lexiconTestCompile 'org.testng:testng:6.8.13'
    lexiconTestCompile group: 'de.dfki.mary', name: 'marytts-client', version: version
}

processLexiconResources {
    from 'lib/modules/en/synthesis', {
        include 'sampa2mrpa_en.map'
    }
    from 'lib/modules/en/us/lexicon', {
        include 'allophones.en_US.xml', '**/*.scm'
    }
    from configurations.lexicon
}

task convertLexica {
    description 'Converts lexicon files from CMUdict scheme format to plain text, with a SAMPA phoneset instead of ARPAbet'
    dependsOn lexiconClasses
    def destFile = file("$sourceSets.lexicon.output.resourcesDir/en.txt")
    inputs.files processLexiconResources
    outputs.files destFile
    doLast {
        javaexec {
            classpath sourceSets.lexicon.runtimeClasspath
            main 'CmuDictConverter'
            def allophoneSetFile = file("$sourceSets.lexicon.output.resourcesDir/allophones.en_US.xml")
            def mappingFile = file("$sourceSets.lexicon.output.resourcesDir/sampa2mrpa_en.map")
            def lexiconFiles = fileTree(sourceSets.lexicon.output.resourcesDir).include('**/*.scm').files
            args = [allophoneSetFile, mappingFile, destFile, lexiconFiles].flatten()
        }
    }
}

task trainLTS(type: JavaExec) {
    description 'Trains LTS rules from converted CMUdict'
    inputs.files file("$sourceSets.lexicon.output.resourcesDir/allophones.en_US.xml"), convertLexica
    args inputs.files
    classpath sourceSets.lexicon.runtimeClasspath
    main 'marytts.tools.transcription.LTSLexiconPOSBuilder'
    doLast {
        copy {
            from sourceSets.lexicon.output.resourcesDir
            include 'en.lextxt',
                    'en.lts',
                    'en_lexicon.dict',
                    'en_lexicon.fst'
            into "$sourceSets.main.output.resourcesDir/marytts/language/en_US/lexicon"
        }
    }
}

processResources.dependsOn trainLTS

trainLTS.dependsOn convertLexica

task lexiconTest(type: Test) {
    useTestNG()
    testClassesDir = sourceSets.lexiconTest.output.classesDir
    classpath = sourceSets.lexiconTest.runtimeClasspath + sourceSets.lexicon.runtimeClasspath
}

lexiconTest.dependsOn convertLexica